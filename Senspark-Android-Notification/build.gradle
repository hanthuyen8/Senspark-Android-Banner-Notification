plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

group = 'com.github.hanthuyen8'
version = '1.1.5'

android {
    namespace 'com.senspark.android.notification'
    compileSdk rootProject.ext.targetSdk

    defaultConfig {
        minSdk 19
        targetSdk rootProject.ext.targetSdk
        versionCode 8
        versionName version

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // For change output file name purpose only
    libraryVariants.configureEach { variant ->
        variant.outputs.configureEach {
            if (variant.name == "release") {
                outputFileName = "${namespace}-${defaultConfig.versionName}.aar"
            } else {
                outputFileName = "${namespace}-${defaultConfig.versionName}-${variant.name}.aar"
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    def emojiVersion = "1.3.0"
    implementation "androidx.emoji2:emoji2:$emojiVersion"
    implementation "androidx.emoji2:emoji2-views:$emojiVersion"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = group
                artifactId = 'Senspark-Android-Banner-Notification'
                version = version
            }
        }
    }
}